%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%% MATERIALS DEFINITION %%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 
% --- UO2 Fuel Material (2.64 wt% enriched uranium)
% --- Density = 10.1 g/cm3 

mat fuel -10.1
    92235.03c -0.02644492
    92238.03c -0.85505247
     8016.03c -0.11850261

% --- Cladding Material (100% natural zirconium)
mat clad -6.55 
    40000.03c -1.0

% --- Light Water, Density=1.0 g/cm3
%     Hydrogen Flagged as a bound scatterer
mat water -1.0 moder MyThermLib_LW 1001
     1001.03c 1.0
     8016.03c 2.0

% --- Heavy Water of Density=1.11 g/cm3
% --- H2 is a bound scatterer with a "moder" card
mat heavywater -1.11 moder MyThermLib 1002
     1002.03c 2
     8016.03c 1


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%% THERMAL SCATTERING DEFINITION %%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

therm MyThermLib_LW lwj3.00t
therm MyThermLib hwj3.00t


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%% SURFACE DEFINITION %%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

pin p1
    fuel 0.4025
    clad 0.4750
    water

surf s1 sqc 0.0 0.0 0.75

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%% CELL DEFINITION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

cell c1 0 fill  p1   -s1
cell c3 0 outside     s1


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%% PARAMETER DEFINITION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 
% --- 5000 neutrons per cycle, 100 active/ 20 inactive cycles

set pop 5000 100 20
set his 1
% set ures 1

% --- Boundary condition (1 = black, 2 = reflective, 3 = periodic)
set bc 2

% --- Set system linear power to 200 W/cm (this is a 2D system)
set power 200

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%% DETECTOR DEFINITION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 
% --- Detector for tallying the flux energy spectrum
% --- 500 bins between 1e-11 MeV to 20 MeV
% --- Type 3 means bins to be equal with in lethargy

det EnergyDetector de MyEnergyGrid
ene MyEnergyGrid 3 500 1e-11 2e1

% --- Detector that calculates the elastic scattering reaction rate in the system
%     Name of the detector is elastic.
%     The detector uses response number -3 (total elastic scattering cross section)
%     of the material at the interaction site (keyword: void)

det elastic dr -3 void

% --- Detector that calculates the capture reaction rate in the system
%     Name of the detector is capture.
%     The detector uses response number -2 (total capture cross section)
%     of the material at the interaction site (keyword: void)

det capture dr -2 void dm fuel

% --- Detector that calculates the radial fission distribution in the fuel: 
%     Name of the detector is RadialFission.
%     The detector uses response number -6 (total fission cross section)
%     of the material at the interaction site (keyword: void)
%     Detector only scores interactions at material "fuel" ("dm fuel").
%     Detector calculates the spatial distribution using a cylindrical mesh "dn 1"
%     with 20 bins in the radial direction between 0.0 and 0.4025 cm ("0.0 0.4025 20")
%     with 1 bin in the angular direction between 0 and 360 degrees ("0 360 1")
%     with 1 bin in the axial direction that extends from -infinity to infinity ("0 0 1")

det RadialFission dr -6 void dm fuel dn 1 0.0 0.4025 200 0 360 1 0 0 1


% --- Detector that calculates the radial capture distribution in the fuel:
%     Name of the detector is RadialCapture.
%     The detector uses response number -2 (total capture cross section)
%     of the material at the interaction site (keyword: void)
%     Detector only scores interactions at material "fuel" ("dm fuel").
%     Detector calculates the spatial distribution using a cylindrical mesh "dn 1"
%     with 20 bins in the radial direction between 0.0 and 0.4025 cm ("0.0 0.4025 20")
%     with 1 bin in the angular direction between 0 and 360 degrees ("0 360 1")
%     with 1 bin in the axial direction that extends from -infinity to infinity ("0 0 1")

det RadialCapture dr -2 void dm fuel dn 1 0.0 0.4025 200 0 360 1 0 0 1

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%% PLOT DEFINITION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

plot 3 200 200
plot 3 1000 1000 0.0 -2.25 2.25 -2.25 2.25
plot 2 200 200

% --- hot shades of orange representing relative fission power
%     cold shades of blue represent thermal flux (flux below 0.625 eV)
mesh 3 200 200



% Darker colors lower values and lighter colors indicate higher values
% --- A meshplot that shows the value of a detector response throughout the geometry (type 8)
%     The colormap that is used is "cold" (colormap 2)
%     The detector linked to this meshplot is named "elastic"
%     Orientation is xy (3), and output size is 200 by 200 pixel

mesh 8 2 elastic 3  200  200


% --- A meshplot that shows the value of a detector response throughout the geometry (type 8)
%     The colormap that is used is "cold" (colormap 2)
%     The detector linked to this meshplot is named "capture"
%     Orientation is xy (3), and output size is 200 by 200 pixel

mesh 8 2 capture 3  200  200